plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.50'
    id 'org.jetbrains.kotlin.kapt' version '1.3.50'
    id 'io.gitlab.arturbosch.detekt' version '1.0.0'
    id 'io.morethan.jmhreport' version '0.9.0'
}

group 'ray.eldath'
version '0.0.1'

sourceCompatibility = 1.11
targetCompatibility = 1.11

ext {
    jmhVersion = '1.21'
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'

    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.5.1'
    // https://mvnrepository.com/artifact/org.openjdk.jmh/jmh-core
    testImplementation group: 'org.openjdk.jmh', name: 'jmh-core', version: jmhVersion
    // https://mvnrepository.com/artifact/org.openjdk.jmh/jmh-generator-annprocess
    kaptTest group: 'org.openjdk.jmh', name: 'jmh-generator-annprocess', version: jmhVersion

    // https://mvnrepository.com/artifact/org.json/json
    implementation group: 'org.json', name: 'json', version: '20180813'
    // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.9'
}

detekt {
    config = files('detekt.yml')
    buildUponDefaultConfig = true

    reports {
        xml { enabled = false }
        html { enabled = false }
    }
}

task jmh(type: JavaExec) {
    main = 'ray.eldath.sirius.test.jmh.JmhTest'
    classpath = sourceSets.test.runtimeClasspath
    defaultCharacterEncoding = 'UTF-8'
}
jmh.finalizedBy tasks.findByName('jmhReport')

test {
    useJUnitPlatform()
    exclude('**/experiment/**')
    exclude('**/jmh/**')
}

repositories {
    if (System.getenv('CI') == null)
        maven { url 'https://maven.aliyun.com/repository/public' }
    jcenter()
    mavenCentral()
}

[compileKotlin, compileTestKotlin].forEach { it.kotlinOptions.jvmTarget = '11' }
[compileJava, compileTestJava].forEach { it.options.encoding = 'UTF-8' }